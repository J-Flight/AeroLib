{
======
Combat
======
}

function getHPBars(): TPointArray;
var
  B     : TBox;
  P     : TPoint;
  i     : Integer;
  tpa,tpa2   : TPointArray;
  atpa,atpa2 : T2DPointArray;
begin
  if not isLoggedIn() then Exit;

  cs_findColorsTolerance(tpa, 65280, 0, AREA_MS, cs_CTS);

  if length(tpa) < 1 then
    exit;

  atpa := clusterTPA(tpa, 1);
  tpa  := [];
  for i:=0 to High(atpa) do
  begin
    B := atpa[i].getBounds();
    B := intToBox(B.X1-1, B.Y1, B.X2+1, B.Y2);
    tpa2 := atpa[i];
    cs_findColorsTolerance(tpa, 255, 0, B, cs_CTS);
    if length(tpa) > 0 then
    begin
      sortTPAFrom(tpa, tpa.midPnt());
      P   := tpa[0];
      tpa := [];
      B   := intToBox(P.X-1, P.Y-2, P.X+28, P.Y+2);
      cs_findColorsTolerance(tpa, 255, 0, B, cs_CTS);
      if length(tpa) > 1 then
      begin
        atpa2 := clusterTPA(tpa, 1);
        tpa   := [];
        sortATPAFromFirstPoint(atpa2, P);
        combineTPAWrap(tpa2, atpa2[0], tpa);
      end;
    end;

    if length(tpa) > 0 then
      B := tpa.getBounds()
    else
      B := atpa[i].getBounds();

    if (inRange(B.Width, 27, 31) and (B.Height = 4)) then
    begin
      setArrayLength(Result, Length(Result)+1);
      Result[High(Result)] := B.MidPoint;
    end;
  end;
end;

function foundHPBarIn(Area: TBox): Boolean;
var
  i    : Integer;
  Bars : TPointArray;
begin
  if not isLoggedIn() then Exit(false);

  Bars := getHPBars();
  if length(Bars) < 1 then
    exit(false);

  for i:=0 to high(Bars) do
    if pointInBox(Bars[i], Area) then
      Exit(true);
end;
